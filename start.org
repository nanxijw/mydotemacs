#+TITLE: Emacs dot file
#+PROPERTY: header-args    :tangle yes
* 文件头
  - 首先在文件开头声明本文件内代码使用词法作用域
    #+begin_src emacs-lisp
      ;;; start.el -*- lexical-binding: t; -*-
    #+end_src
  - 以服务器模式启动
    #+begin_src emacs-lisp :tangle no
      (server-start)
    #+end_src
  - 定义一些后面要用到的变量
    无
  - 定义一些后面要用到的函数
    #+begin_src emacs-lisp :tangle no
      (defun version> (v1 v2) (version< v2 v1))
      (defun version>= (v1 v2) (version<= v2 v1))
    #+end_src
  - 设置custom文件地址，并载入
    #+begin_src emacs-lisp
      (setq custom-file (locate-user-emacs-file "custom.el"))
      (when (file-exists-p custom-file)
        (load custom-file))
    #+end_src
  - 可执行文件路径
    #+begin_src emacs-lisp :tangle no
      (add-to-list 'exec-path "C:/link")
    #+end_src
  - 解决一些命令行工具乱码问题
    #+begin_src emacs-lisp
      (modify-coding-system-alist 'process "[cC][mM][dD][pP][rR][oO][xX][yY]" '(utf-8 . gbk-dos))
    #+end_src
* 包管理器
  - 设置使用国内镜像
    #+BEGIN_SRC emacs-lisp
      ;; MelpaPackages
      ;; Select the folder to store packages
      ;; Comment / Uncomment to use desired sites
      (setq package-user-dir (expand-file-name "elpa" user-emacs-directory)
            package-archives
            '(
              ;; ("gnu"   . "https://elpa.gnu.org/packages/")
              ;; ("melpa" . "https://melpa.org/packages/")
              ;; ("cselpa" . "https://elpa.thecybershadow.net/packages/")
              ("gnu-tuna"   . "http://mirrors.tuna.tsinghua.edu.cn/elpa/gnu/")
              ("melpa-tuna" . "http://mirrors.tuna.tsinghua.edu.cn/elpa/melpa/")
              ;; ("melpa-cn" . "http://mirrors.cloud.tencent.com/elpa/melpa/")
              ;; ("gnu-cn"   . "http://mirrors.cloud.tencent.com/elpa/gnu/")
              ))
      ;; -MelpaPackages

      ;; ConfigurePackageManager
      (unless (bound-and-true-p package--initialized)
        (setq package-enable-at-startup nil)          ; To prevent initializing twice
        (package-initialize))
    #+END_SRC
  - setup.el设置
    #+begin_src emacs-lisp
      (eval-and-compile
        (unless (require 'setup nil t)
          (package-install 'setup)
          (require 'setup))

        (defmacro setc (&rest args)
          "Customize user options using ARGS like `setq'."
          (declare (debug setq))
          `(setup (:option ,@args)))

        (setup-define :autoload
          (lambda (func)
            (let ((fn (if (memq (car-safe func) '(quote function))
                          (cadr func)
                        func)))
              `(unless (fboundp (quote ,fn))
                 (autoload (function ,fn) ,(symbol-name (setup-get 'feature)) nil t))))
          :documentation "Autoload COMMAND if not already bound."
          :repeatable t
          :signature '(FUNC ...))

        (setup-define :needs
          (lambda (executable)
            `(unless (executable-find ,executable)
               ,(setup-quit)))
          :documentation "If EXECUTABLE is not in the path, stop here."
          :repeatable 1)

        (setup-define :load-from
          (lambda (path)
            `(let ((path* (expand-file-name (locate-user-emacs-file ,path))))
               (if (file-exists-p path*)
                   (add-to-list 'load-path path*)
                 ,(setup-quit))))
          :documentation "Add PATH to load path.
        This macro can be used as NAME, and it will replace itself with
        the nondirectory part of PATH.
        If PATH does not exist, abort the evaluation."
          :shorthand (lambda (args)
                       (intern
                        (file-name-nondirectory
                         (directory-file-name (cadr args)))))))
    #+end_src
* UI界面
  - 隐藏菜单栏、工具栏、滚动条，设置边框宽度
    放到eraly-init.el中去了，这屏蔽
    #+BEGIN_SRC emacs-lisp :tangle no
      (menu-bar-mode -1)
      (tool-bar-mode -1)
      (scroll-bar-mode -1)
    #+END_SRC
  - 主题设置
    #+BEGIN_SRC emacs-lisp
      (setup (:package srcery-theme)
        (load-theme 'srcery))

      ;; (setup (:package solarized-dark)
      ;;   (load-theme 'solarized-dark))
    #+END_SRC
  - 开启自动换行，程序标题栏显示当前标记的文件名
    #+BEGIN_SRC emacs-lisp
      (setq-default
       truncate-lines t
       frame-title-format "%b    %Z    %f"
       ) ;; end of setq-default
    #+END_SRC
  - modeline设置
    + 显示行号、列号以及当前文件的总字符数
      #+BEGIN_SRC emacs-lisp
        (setc line-number-mode t
              column-number-mode t
              size-indication-mode t)
      #+END_SRC
  - 高亮当前行
    #+BEGIN_SRC emacs-lisp
      (setc global-hl-line-mode t)
    #+END_SRC
  - 高亮配对的括号
    #+begin_src emacs-lisp
      (setc show-paren-mode t
            show-paren-when-point-in-periphery t)
    #+end_src
  - 使用isearch搜索时，显示当前匹配项的数量
    #+BEGIN_SRC emacs-lisp
      (setc isearch-lazy-count t
            lazy-count-prefix-format "%s/%s ")
    #+END_SRC
  - whitespace设置
    #+BEGIN_SRC emacs-lisp
      (setc global-whitespace-mode t)
      (add-hook 'before-save-hook #'delete-trailing-whitespace)

      ;; Don't use different background for tabs.
      (face-spec-set 'whitespace-tab
                     '((t :background unspecified)))

      ;; Only use background and underline for long lines, so we can still have
      ;; syntax highlight.

      ;; For some reason use face-defface-spec as spec-type doesn't work.  My guess
      ;; is it's due to the variables with the same name as the faces in
      ;; whitespace.el.  Anyway, we have to manually set some attribute to
      ;; unspecified here.
      (face-spec-set 'whitespace-line
                     '((((background light))
                        :background "#d8d8d8" :foreground unspecified
                        :underline t :weight unspecified)
                       (t
                        :background "#404040" :foreground unspecified
                        :underline t :weight unspecified)))

      ;; Use softer visual cue for space before tabs.
      (face-spec-set 'whitespace-space-before-tab
                     '((((background light))
                        :background "#d8d8d8" :foreground "#de4da1")
                       (t
                        :inherit warning
                        :background "#404040" :foreground "#ee6aa7")))

      (setq whitespace-line-column nil
            whitespace-style '(face             ; visualize things below:
                               empty            ; empty lines at beginning/end of buffer
                               ;; lines-tail       ; lines go beyond `fill-column'
                               space-before-tab ; spaces before tab
                               trailing         ; trailing blanks
                               tabs             ; tabs (show by face)
                               tab-mark))       ; tabs (show by symbol)

    #+END_SRC
  - 设置鼠标滚轮一次滚动3行，使得滚动不会那么跳跃
    #+BEGIN_SRC emacs-lisp
      ;; scroll 3 line at a time (less "jumpy" than defaults)
      (setq mouse-wheel-scroll-amount '(3 ((shift) . 1)) ;; 3 line at a time
            mouse-wheel-progressive-speed nil ;; don't accelerate scrolling
            mouse-wheel-follow-mouse 't ;; scroll window under mouse
            scroll-margin 3) ;; scroll-margin 3 靠近屏幕边沿3行时开始滚动，可以很好的看到上下文
    #+END_SRC
  - 字体设置
    #+BEGIN_SRC emacs-lisp :tangle yes
      ;; Auto generated by cnfonts
      ;; <https://github.com/tumashu/cnfonts>
      (set-face-attribute
       'default nil
       :font (font-spec :name "Cascadia Code"
                        :weight 'normal
                        :slant 'normal
                        :size 12.0))
      (dolist (charset '(kana han symbol cjk-misc bopomofo))
        (set-fontset-font
         (frame-parameter nil 'font)
         charset
         (font-spec :name "微软雅黑"
                    :weight 'normal
                    :slant 'normal
                    :size 13.5)))
    #+END_SRC
* 个人使用习惯方面的设置
  - 粘贴时覆盖选中的region
    #+BEGIN_SRC emacs-lisp
      (setc delete-selection-mode t)
    #+END_SRC
  - 不锁定文件
    编辑文件时emacs会自动创建一个 ==.#== 的文件，在windows系统下会导致一些奇怪的问题，这里设置为不创建这个文件
    #+BEGIN_SRC emacs-lisp
      (setq create-lockfiles nil)
    #+END_SRC
  - 需要输入yes的时候，只输入y
    #+BEGIN_SRC emacs-lisp
      (defalias 'yes-or-no-p 'y-or-n-p)
    #+END_SRC
  - 因为我平时电脑都不开声音，因此让bell可视化
    #+begin_src emacs-lisp
      (setq visible-bell t)
    #+end_src
  - 使用ibuffer
    #+BEGIN_SRC emacs-lisp
      (global-set-key (kbd "C-x C-b") 'ibuffer)
    #+END_SRC
  - 使用F3查找光标当前所在位置的symbol
    #+begin_src emacs-lisp
      (setup (:package symbol-overlay)
        (:global "C-<f3>" symbol-overlay-put
                 "<f3>" symbol-overlay-jump-next
                 "S-<f3>" symbol-overlay-jump-prev
                 "C-S-<f3>" symbol-overlay-remove-all))
    #+end_src
    高亮一个symbol后，光标在该symbol上时会自动进入symbol-mode，symbol-overlay-map中快捷键具体如下：
    | i | symbol-overlay-put                | 高亮或取消高亮当前symbol      |
    | n | symbol-overlay-jump-next          | 跳转到下一个位置              |
    | p | symbol-overlay-jump-prev          | 跳转到上一个位置              |
    | w | symbol-overlay-save-symbol        | 复制当前symbol                |
    | t | symbol-overlay-toggle-in-scope    | 切换高亮范围到作用域          |
    | e | symbol-overlay-echo-mark          | 撤销上一次跳转                |
    | d | symbol-overlay-jump-to-definition | 跳转到定义                    |
    | s | symbol-overlay-isearch-literally  | 切换为isearch并搜索当前symbol |
    | q | symbol-overlay-query-replace      | 查找替换当前symbol            |
    | r | symbol-overlay-rename             | 对symbol直接重命名            |
  - 在window间移动
    #+BEGIN_SRC emacs-lisp :tangle no
      (when (eq 'windows-nt system-type)
        (setq w32-lwindow-modifier 'super) ;; 设置win键为super键
        (setq w32-rwindow-modifier 'super) ;; 设置win键为super键
        (global-set-key (kbd "M-s-<left>") 'windmove-left)
        (global-set-key (kbd "M-s-<right>") 'windmove-right)
        (global-set-key (kbd "M-s-<up>") 'windmove-up)
        (global-set-key (kbd "M-s-<down>") 'windmove-down))
    #+END_SRC
  - kill-ring时，若没有选中region，则复制当前行
    #+BEGIN_SRC emacs-lisp
      (defun my-kill-ring-save (beg end &optional region)
        (interactive (list (mark) (point)
                           (prefix-numeric-value current-prefix-arg)))
        (if (region-active-p)
            (kill-ring-save beg end region)
          (progn
            (message "Copied line")
            (kill-ring-save (line-beginning-position) (line-end-position)))))

      (global-set-key [remap kill-ring-save] 'my-kill-ring-save)
    #+END_SRC
  - 记录上次关闭前，光标在文件中的位置
    #+BEGIN_SRC emacs-lisp
      (setc save-place-mode t
            save-place-file (locate-user-emacs-file "tmp/places"))
    #+END_SRC
  - 自动读取外部文件对正在编辑的文件的修改
    #+BEGIN_SRC emacs-lisp
      (setc global-auto-revert-mode t)
    #+END_SRC
  - 单行内容过长时关闭一些mode
    有时候会打开一些文件，这些文件里的某一行特别长，而Emacs没有针对这种情况做特殊 处理，会导致整个界面卡死。这里启用so-long，当打开一个具有长行的文件时，它会自动检测并将一些可能导致严重性能的mode关闭， 如font-lock (syntax highlight)。
    #+BEGIN_SRC emacs-lisp
      (setc global-so-long-mode t)
    #+END_SRC
  - 括号、引号自动配对补全
    #+begin_src emacs-lisp :tangle no
      (setc electric-pair-mode t)
    #+end_src
  - 搜索时大小写敏感
    #+begin_src emacs-lisp
      (setq case-fold-search t)
    #+end_src
* projectile
  #+BEGIN_SRC emacs-lisp
    (setup (:package projectile)
      (:autoload projectile-project-root))
  #+END_SRC
* 自动补全
  - YASnippet
    YASnippet, a programming template system for Emacs. It loads YASnippet Snippets, a collection of yasnippet snippets for many languages.
    #+BEGIN_SRC emacs-lisp
      (setup (:package yasnippet)
        (:option yas-snippet-dirs (list (expand-file-name (locate-user-emacs-file "etc/snippets"))))
        (:with-mode yas-minor-mode
          (:unbind "TAB" [(tab)])
          (:hook-into prog-mode))

        ;; Created 里面用到了calendar-month-name
        (:with-feature calendar
          (:autoload calendar-month-name)))
    #+END_SRC
  - corfu
    #+BEGIN_SRC emacs-lisp
      (setup (:package corfu)
        (:option corfu-auto t ;; Enable auto completion
                 corfu-history-mode t
                 corfu-quit-at-boundary t ;; Automatically quit at word boundary
                 corfu-quit-no-match t ;; Automatically quit if there is no match
                 corfu-preview-current nil
                 corfu-echo-documentation nil
                 corfu-auto-delay 1.0
                 corfu-auto-prefix 5)
        (:bind-into corfu-map
          "C-d" corfu-info-documentation
          "C-s" corfu-info-location)
        (:hook-into prog-mode))

      (setup (:package cape)
        (add-to-list 'completion-at-point-functions #'cape-dabbrev)
        (add-to-list 'completion-at-point-functions #'cape-abbrev)
        (add-to-list 'completion-at-point-functions #'cape-keyword))
    #+END_SRC
  - abbrev
    #+begin_src emacs-lisp
      (setup dabbrev
        ;; Swap M-/ and C-M-/
        (:global "M-/" dabbrev-completion
                 "C-M-/" dabbrev-expand))
    #+end_src
* 文件备份
  #+BEGIN_SRC emacs-lisp
    (setq auto-save-list-file-name (locate-user-emacs-file "tmp/autosave-list")
          auto-save-file-name-transforms `((".*" ,(locate-user-emacs-file "tmp/autosaves/") t t)))

    (setq backup-by-copying t) ;; 使用复件备份方式
    (setq backup-directory-alist `((".*" . ,(locate-user-emacs-file "tmp/backups")))) ;; 设置备份路径

    ;; 设置一下备份时的版本控制，这样更加安全。
    (setq version-control     t ;; 启用版本控制，即可以备份多次
          kept-new-versions   32 ;; 保留最新的32个版本
          kept-old-versions   8 ;; 备份最原始的8个版本，即第一次编辑前的文档，和第二次编辑前的文档...
          delete-old-versions t ;; 删除中间版本
          )

    ;; 最近访问文件列表
    (setq recentf-max-saved-items 100)
    (setq recentf-save-file (locate-user-emacs-file "tmp/recentf"))
    (with-eval-after-load 'consult
      (recentf-mode +1))
    #+END_SRC
* 搜索功能
  - 使用color-rg搜索
    #+BEGIN_SRC emacs-lisp
      (setup color-rg
        (:load-from "lisp/color-rg")
        (:option color-rg-max-column 1000)
        (:autoload color-rg-search-project
                   color-rg-read-input
                   color-rg-search-input)

        (:when-loaded
          (defun color-rg-project-root-dir ()
            (let ((dir (projectile-project-root)))
              (if dir dir
                default-directory))))

        (modify-coding-system-alist 'process "rg" '(utf-8 . gbk-dos))

        (defun my-color-rg-search-in-directory ()
          (interactive)
          (let ((directory (read-directory-name "In Directory:"))
                (keyword (color-rg-read-input)))
            (color-rg-search-input keyword (expand-file-name directory))))
        (:global "C-c s f" my-color-rg-search-in-directory
                 "C-c /" color-rg-search-project))
    #+END_SRC
  - vertico
    #+BEGIN_SRC emacs-lisp
      (setup (:package vertico vertico-posframe)
        ;; Vertico
        (:option vertico-mode t)
        ;; Vertico posframe
        (:option vertico-posframe-mode t))
    #+END_SRC
  - consult
    #+BEGIN_SRC emacs-lisp
      (setup (:package consult)
        (:option consult-project-root-function #'projectile-project-root
                 consult-preview-key nil ;; 关闭预览
                 )
        (:global [remap switch-to-buffer] #'consult-buffer
                 [remap goto-line] #'consult-goto-line
                 "C-c C-s" consult-line
                 "C-c s j" consult-imenu
                 "C-c f r" consult-recent-file
                 "C-c f d" consult-fd
                 "C-M-y" consult-yank-pop)

        (:when-loaded
          (defvar consult--fd-command nil)
          (defun consult--fd-builder (input)
            (unless consult--fd-command
              (setq consult--fd-command
                    (if (eq 0 (call-process-shell-command "fdfind"))
                        "fdfind"
                      "fd")))
            (pcase-let* ((`(,arg . ,opts) (consult--command-split input))
                         (`(,re . ,hl) (funcall consult--regexp-compiler
                                                arg 'extended t)))
              (when re
                (list :command (append
                                (list consult--fd-command
                                      "--color=never" "--full-path"
                                      (consult--join-regexps re 'extended))
                                opts)
                      :highlight hl))))

          (defun consult-fd (&optional dir initial)
            (interactive "P")
            (let* ((prompt-dir (consult--directory-prompt "Fd" dir))
                   (default-directory (cdr prompt-dir)))
              (funcall #'find-file (consult--find (car prompt-dir) #'consult--fd-builder initial))))

          ;; 让fd支持gbk
          (modify-coding-system-alist 'process "fd" '(utf-8 . gb18030-dos))))
    #+END_SRC
  - 使用orderless进行过滤和排序补全选项
    #+begin_src emacs-lisp
      ;; Optionally use the `orderless' completion style. See
      ;; `+orderless-dispatch' in the Consult wiki for an advanced Orderless style
      ;; dispatcher. Additionally enable `partial-completion' for file path
      ;; expansion. `partial-completion' is important for wildcard support.
      ;; Multiple files can be opened at once with `find-file' if you enter a
      ;; wildcard. You may also give the `initials' completion style a try.
      (setup (:package orderless)
        ;; Configure a custom style dispatcher (see the Consult wiki)
        ;; (setq orderless-style-dispatchers '(+orderless-dispatch)
        ;;       orderless-component-separator #'orderless-escapable-split-on-space)
        (:option completion-styles '(orderless basic)
                 completion-category-defaults nil
                 completion-category-overrides '((file (styles basic partial-completion)))))
    #+end_src
* 编程相关设置
  - 使用4个空格代替tab
    #+BEGIN_SRC emacs-lisp
    (setq-default tab-width 4 indent-tabs-mode nil)
    (add-hook 'c-mode-common-hook
              (lambda ()
                (c-set-style "stroustrup")))
    #+END_SRC
** P01
   - 由于历史原因，项目默认使用gbk编码
     #+BEGIN_SRC emacs-lisp :tangle no
       (prefer-coding-system 'chinese-gbk-dos)
     #+END_SRC
   - 使用pike-mode来编辑项目脚本，因为pike-mode隶属于cc-mode包，因此这里使用cc-mode来设置
     #+BEGIN_SRC emacs-lisp :tangle no
       (setup pike-mode
         (:file-match "/server_scripts/.+\\.[ch]$")
         (:hook (lambda ()
                    (set (make-local-variable 'imenu-generic-expression)
                         (list
                          (list nil "^\\<[^()\n]*[^[:alnum:]_:<>~]\\([[:alpha:]_][[:alnum:]_:<>~]*\\)\\([     \n]\\|\\\\\n\\)*(\\([   \n]\\|\\\\\n\\)*\\([^   \n(*][^()]*\\(([^()]*)[^()]*\\)*\\)?)\\([   \n]\\|\\\\\n\\)*[^  \n;(]" 1)))

                    (setq c-multiline-string-start-char nil)

                    (define-key pike-mode-map [(f2)] 'p01/id-text-at-point)
                    (define-key pike-mode-map "," nil) ;; 输入“,”时不重新格式化代码
                    (define-key pike-mode-map "(" nil) ;; 输入“(”时不重新格式化代码
                    (define-key pike-mode-map ")" nil) ;; 输入“)”时不重新格式化代码
                    (define-key pike-mode-map ":" nil) ;; 输入“:”时不重新格式化代码
                    (define-key pike-mode-map ";" nil) ;; 输入“;”时不重新格式化代码
                    (define-key pike-mode-map "/" nil) ;; 输入“/”时不重新格式化代码
                    )))
     #+END_SRC
   - 使用conf-mode打开list文件
     #+BEGIN_SRC emacs-lisp :tangle no
       (setup conf-mode
         (:file-match "\\.list$"))
     #+END_SRC
  - citre
    #+begin_src emacs-lisp :tangle no
      (setup (:package citre)
        (:option citre-enable-imenu-integration nil

                 ;; Set these if readtags/ctags is not in your path.
                 citre-readtags-program "/path/to/readtags"
                 citre-ctags-program "/path/to/ctags"

                 ;; Set this if you use project management plugin like projectile.  It's
                 ;; used for things like displaying paths relatively, see its docstring.
                 citre-project-root-function #'projectile-project-root

                 ;; Set this if you want to always use one location to create a tags file.
                 ;; citre-default-create-tags-file-location 'global-cache

                 ;; See the "Create tags file" section above to know these options
                 citre-use-project-root-when-creating-tags t

                 ;; citre-prompt-language-for-ctags-command t
                 ;; By default, when you open any file, and a tags file can be found for it,
                 ;; `citre-mode' is automatically enabled.  If you only want this to work for
                 ;; certain modes (like `prog-mode'), set it like this.
                 citre-auto-enable-citre-mode-modes '(prog-mode)

                 citre-enable-capf-integration t
                 citre-capf-optimize-for-popup t)

        ;; This is needed in `:init' block for lazy load to work.
        (require 'citre-config)
        ;; Bind your frequently used commands.  Alternatively, you can define them
        ;; in `citre-mode-map' so you can only use them when `citre-mode' is enabled.
        (global-set-key (kbd "M-.") 'citre-jump)
        (global-set-key (kbd "M-,") 'citre-jump-back)
        (global-set-key (kbd "C-c t j") 'citre-jump)
        (global-set-key (kbd "C-c t J") 'citre-jump-back)
        (global-set-key (kbd "C-c t u") 'citre-update-this-tags-file)
        (global-set-key (kbd "C-c t p") 'citre-peek)

        (:when-loaded
          (defvar citre-lang-pike-plist
            `(:completion-filter
              citre-lang-pike-completion-filter
              "pike language support for Citre."))

          (defvar citre-lang-pike-completion-filter
            (citre-core-filter 'typeref "typename:private" 'prefix nil t t))

          (setf (alist-get 'pike-mode citre-language-support-alist)
                citre-lang-pike-plist)))
    #+end_src
    ctags 命令
    #+begin_src text :tangle no
      ctags
      -o
      %TAGFILE%
      --languages=c++
      --kinds-c++=df
      --langmap=c++:.c.h
      -h .h.c
      --fields=*
      --extras=*
      --input-encoding=GBK
      --exclude=xgs/*
      -R
    #+end_src
* 临时实验配置
* 快捷键设置
  - 取消一些我不用的快捷键
    #+begin_src emacs-lisp
      (global-set-key (kbd "C-z") nil)
    #+end_src
* 性能优化
  - 使用gcmh来管理gc
    #+begin_src emacs-lisp
      (setup gcmh
        (:package gcmh)
        (:option gcmh-mode t
                 gcmh-high-cons-threshold 16777216))
    #+end_src
